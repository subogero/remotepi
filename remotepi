#!/usr/bin/perl
use Mojolicious::Lite;
use Mojo::IOLoop;
use IPC::Open2;
use WWW::U2B;

# STARTUP
sub statusdiff; sub status; sub thumbnail; sub decorate; sub logger;
sub ls;
sub rpifm_my; sub run_rpifm; sub byalphanum;
sub yt;
my ($root, $ytid, %ythits, $fm_my);
our ($img_dir, $img);
# Clean up albumart symlink upon exit
$SIG{TERM} = sub { thumbnail };

# Check user
my @userdata = getpwuid $< or die "Unkown user id $<";
my ($user, $home) = @userdata[0, -2];

# Run in /var/tmp/remotepi or ~/.remotepi
my $cwd = $user eq 'root' ? '/var/tmp/remotepi' : "$home/.remotepi";
-d $cwd or mkdir $cwd;
chdir $cwd;

# Start own omxd instance unless running as root
system "omxd" if $user ne 'root';

# Get ls root directory
my $cfgfile = $user eq 'root' ? '/etc/remotepi.conf' : "$home/.remotepi.conf";
$root = '/home';
if (open my $cfg, $cfgfile) {
    while (<$cfg>) {
        chomp;
        $root = $1 if /rootdir=(.*)/ && -d $1;
    }
}

# ROUTES
get '/' => sub {
    my $c = shift;
    $c->reply->static('index.html');
};

get "/thumbs*thumb" => { thumb => '/' } => sub {
    my $c = shift;
    logger 'Thumb route fund!';
    if ($img) {
        $c->reply->static($img)
    } else {
        $c->render(template => 'not_found');
    }
};

any '/S*param' => { param => '' } => sub {
    my $c = shift;
    my $data = $c->req->json;
    $c->render(json => status($c->stash('param'), $data));
    statusdiff if $data;
};

any '/home*param' => { param => '/' } => sub {
    my $c = shift;
    my $data = $c->req->json;
    $c->render(json => ls($c->stash('param'), $data));
};

any '/fm*param/' => { param => '/' } => sub {
    my $c = shift;
    my $data = $c->req->json;
    my ($response, $code) = fm($c->stash('param'), $data);
    if ($code) {
        $c->render(text => $response, status => $code);
    } else {
        $c->render(json => $response);
    }
    rpifm_my;
};

any '/yt*param' => { param => '/' } => sub {
    my $c = shift;
    my $data = $c->req->json;
    $c->render(json => yt($c->stash('param'), $data));
};

my %clients;
my $st_old;
my $loop;
websocket '/diff' => sub {
    my $c = shift;
    my $id = sprintf "%s", $c->tx;
    $loop = Mojo::IOLoop->recurring(1 => \&statusdiff) unless keys %clients;
    $clients{$id} = $c->tx;
    my $n = keys %clients;
    app->log->debug("connection opened, $n clients");
    $c->on(json => sub {
        my $n = keys %clients;
        app->log->debug("new connection, $n clients");
        $clients{$id}->send({ json => $st_old })
    });
    $c->on(finish => sub {
        delete $clients{$id};
        my $n = keys %clients;
        Mojo::IOLoop->remove($loop) unless $n;
        app->log->debug("connection closed, $n clients remain");
    });
};

rpifm_my;

# START APP
push @{ app->static->paths }, $cwd;
my $port = shift // 31100;
app->start('daemon', '-l' => "http://*:$port");

# HELPERS
# Get diff between status 1s before and now, send to websocket clients
sub statusdiff {
    my $st_new = status;
    $st_old //= $st_new;
    my $diff = {};
    foreach my $key (qw(doing at of what image)) {
        $st_new->{$key} //= '';
        if ($st_new->{$key} ne $st_old->{$key}) {
            $diff->{$key} = $st_new->{$key};
            $diff->{of} = $st_new->{of} if $key eq 'at';
            $diff->{image} = $st_new->{image} if $key eq 'what';
        }
    }
    if (@{$st_new->{list}} != @{$st_old->{list}}) {
        $diff->{list} = $st_new->{list};
    } else {
        for (0 .. scalar @{$st_new->{list}} - 1) {
            next if $st_new->{list}[$_]{label} eq $st_old->{list}[$_]{label};
            $diff->{list} = $st_new->{list};
            last;
        }
    }
    $st_old = $st_new;
    # Send diff to all clients
    foreach (keys %clients) {
        $clients{$_}->send({ json => $diff });
    }
}

# Print playlist status
sub status {
    my $param = shift // '';
    my $data = shift;
    if ($data && $data->{cmd} =~ /^[NRr.pPfFnxXhjdDg]$/) {
        `omxd $data->{cmd} $data->{file}`;
        return {};
    }
    unless (open PLAY, "omxd S all |") {
        return;
    }
    my $now = <PLAY>;
    chomp $now;
    my ($doing, $at, $of, $what) = split /[\s\/]/, $now, 4;
    # Remove root from track name if local file
    my ($dir) = $what =~ m|^(/.+)/[^/]+$|;
    $what =~ s/$root//;
    $what = decorate $param, $what;
    # Construct JSON response
    my $response = { doing => $doing, at => $at+0, of => $of+0, what => $what };
    my $i = 0;
    @{$response->{list}} = map {
        chomp;
        s/^> //;
        s/^$root(.+)/$1/;
        $_ = decorate 'post', $_;
        { name => $i++, label => $_, ops => [ qw(g x) ] }
    } <PLAY>;
    close PLAY;

    $response->{image} = "thumbs/$img" if thumbnail $dir;
    return $response;
}

# Decorate currect track with FM/YouTube info
sub decorate {
    my $param = shift;
    my $what = shift;
    # Replace track name with internet radio if needed
    foreach (keys %$fm_my) {
        next unless $fm_my->{$_}{listen} eq $what;
        my $url = $_;
        $what = "rpi.fm://$fm_my->{$_}{title}";
        if ($param =~ m|\d*/details|) {
            my $st_page = `curl -L "internet-radio.com/search/?radio=$url" 2>/dev/null`;
            $st_page =~ m|<br>[\s\n]*<b>(.+?)</b>|s;
            $what .= "\n$url\n$1";
        }
    }
    # Replace track name with YouTube id if needed
    $what =~ s|.*\.u2bfifo$|YouTube://$ythits{$ytid}|;

    return $what;
}

# Get thumbnail image link from current playback directory
# GET /thumbs/$img -> /var/tmp/remotepi/$img -> $dir/$img
sub thumbnail {
    my $dir = shift;
    return $img if $dir && $img_dir && $dir eq $img_dir;
    unlink $img if $img;
    $img = '';
    $img_dir = $dir;

    return $img unless $dir && opendir DIR, $dir;

    my @imgs = sort grep { /(png|jpe?g)$/i && $_ ne 'rpi.jpg' } readdir DIR;
    closedir DIR;
    return $img unless @imgs;

    $img = $imgs[0];
    symlink "$dir/$img", $img or logger "Unable to symlink $img";
    return $img;
}

# Browse Raspberry Pi
sub ls {
    my $dir = shift;
    my $data = shift;
    if ($data) {
        `omxd $data->{cmd} "$root$data->{file}"` if $data->{cmd} =~ /[iaAIHJ]/;
        return [];
    }
    # Return to root dir upon dangerous attempts
    $dir = $dir =~ /^\.|^$/ ? $root : "$root$dir";
    # Sanitize dir: remove double slashes, cd .. until really dir
    $dir =~ s|(.+)/.+|$1| while ! -d $dir;
    opendir DIR, $dir;
    my @files;
    push @files, $_ while readdir DIR;
    closedir DIR;
    my $response = [];
    foreach (sort { -d "$dir/$a" && -f "$dir/$b" ? -1
                  : -f "$dir/$a" && -d "$dir/$b" ?  1
                  :          $a     cmp      $b     } @files) {
        next if /^\.$/;
        next if /^\.\w/;
        next if /^\.\.$/ && $dir eq "$root/";
        if ($_ eq '..') {
            push @$response, { name => $_, ops => [ qw(cd) ] };
        } elsif (-d "$dir/$_") {
            push @$response, { name => $_, ops => [ qw(cd i a A) ] };
        } else {
            push @$response, { name => $_, ops => [ qw(i a A I H J) ] };
        }
    }
    return $response;
}

# Browse internet radio stations
sub fm {
    # Convert both URL and JSON list into multiline strings
    (my $cmd = shift) =~ s|^/||;
    $cmd =~ s|/|\n|g;
    my $data = shift;
    if (ref $data eq 'ARRAY') {
        $data = join("\n", @$data);
    }
    if ($cmd =~ /\n[iaAIHJ]/) {
        return "400 No playlist changes in GET requests", 400;
    }
    my $cmds = $data || $cmd;

    my $response = [
        { name => '/g', ops => [ 'cd' ], label => 'Genres' },
        { name => '/m', ops => [ 'cd' ], label => 'My Stations' },
    ];
    my ($title, %list) = run_rpifm $cmds;
    push @$response, { name => $title || 'Genres', ops => [] } if %list;
    foreach (sort byalphanum keys %list) {
        unless ($title) {
            push @$response, {
                name => $_,
                ops => [ 'cd' ],
                label => $list{$_},
            };
        } elsif (/^[<>]$/) {
            push @$response, {
                name => $_,
                ops => [ 'cd' ],
                label => $_ eq '<' ? 'Previous' : 'Next',
            };
        } else {
            push @$response, {
                name => $_,
                ops => [ qw(i a A I H J) ],
                label => $list{$_},
            };
        }
    }
    return $response;
}

sub run_rpifm {
    my $cmd = shift;
    # Force rpi.fm to store status in current dir
    local %ENV;
    delete $ENV{HOME};
    my $pid = open2(\*IN, \*OUT, '/usr/bin/rpi.fm') or die $!;
    print OUT $cmd;
    close OUT;
    my ($title, %list);
    while (<IN>) {
        s/\r|\n//g;
        if (/^[a-zA-Z]/) {
            $title = $_;
            %list = ();
        } elsif (/^ *(\d+|[<>]) +(.+)$/) {
            $list{$1} = $2;
        }
    }
    close IN;
    waitpid $pid, 0;
    return $title, %list;
}

sub rpifm_my {
    my $cfgfile = ".rpi.fm";
    return unless open RPI, $cfgfile;
    my $dump;
    while (<RPI>) {
        $dump .= $_ if /MyStations/ || $dump; # Lines from MysStations on
        last if $dump && /^  }/;              # until its closing brace
    }
    $dump =~ s/.+?\{/\$fm_my = {/;
    eval $dump;
}

sub byalphanum {
    return $a <=> $b if $a =~ /^\d+$/ && $b =~ /^\d+$/;
    return $a cmp $b;
}

# Browse and play YouTube
sub yt {
    (my $cmd = shift) =~ s|^/||;
    $cmd =~ m|^([^/]+)/(.*)|;
    my $query;
    ($cmd, $query) = ($1, $2);

    my $data = shift;

    # Playback command
    if ($data) {
        my @streams = WWW::U2B::extract_streams $data->{query};
        foreach (@streams) {
            next unless $_->{extension} eq 'mp4';
            WWW::U2B::playback "omxd $data->{cmd}", $_;
            logger "U2B: extension=".$_->{extension}.", quality=".$_->{quality};
            logger "omxd $data->{cmd} $_->{url}";
            last;
        }
        $ytid = $data->{query};
        return '';
    }
    # Search command
    my @response = WWW::U2B::search(split / /, $query);
    foreach (@response) {
        $_->{thumbnail} =~ s/https:/http:/;
        $_->{ops} = [ qw(I H J) ];
        $ythits{$_->{name}} = $_->{label};
    }
    return \@response;
}

sub logger {
    print "@_\n";
}

__DATA__

@@ not_found.html.ep
404 Not found
